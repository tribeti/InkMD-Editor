; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

#define MyAppName "InkMD Editor"
#define MyAppVersion "0.1.1"
#define MyAppPublisher "tribeti"
#define MyCertFile "InkMD_Editor_0.1.0.0_x64_Debug.cer"
#define MyAppBundle "InkMD_Editor_0.1.0.0_x64_Debug.msixbundle"

[Setup]
AppId={{6FB634CF-7009-4E4F-A592-D325C4D3DC57}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={tmp}
DisableProgramGroupPage=yes
PrivilegesRequired=admin
OutputBaseFilename=InkMD_Editor_Setup
Compression=lzma2
SolidCompression=yes
MinVersion=10.0.17763
Uninstallable=no 

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "D:\InkMD_Editor_0.1.0.0_Debug_Test\*"; DestDir: "{tmp}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Registry]
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\AppModelUnlock"; ValueType: dword; ValueName: "AllowAllTrustedApps"; ValueData: "1"; Flags: createvalueifdoesntexist
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\AppModelUnlock"; ValueType: dword; ValueName: "AllowDevelopmentWithoutDevLicense"; ValueData: "1"; Flags: createvalueifdoesntexist

[Run]
Filename: "certutil.exe"; \
    Parameters: "-addstore -f ""Root"" ""{tmp}\{#MyCertFile}"""; \
    Flags: runhidden waituntilterminated; \
    StatusMsg: "Installing Security Certificate..."

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -Command ""Add-AppxPackage -Path '{tmp}\{#MyAppBundle}' -ForceUpdateFromAnyVersion"""; \
    Flags: runhidden waituntilterminated; \
    StatusMsg: "Installing Application..."

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    MsgBox('Successfully installed! You can find InkMD Editor in your Start Menu.', mbInformation, MB_OK);
  end;
end;